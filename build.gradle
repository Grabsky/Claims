plugins {
    id("java-library")
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("de.eldoria.plugin-yml.paper") version "0.7.1"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.17"
}

final String VERSION = "1.21.7"
final String RUN_NUMBER = System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"

group = "cloud.grabsky"
version = "$VERSION+$RUN_NUMBER"

repositories {
    mavenLocal()
    mavenCentral()
    // Repository for the Paper API.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository for the WorldEdit and WorldGuard APIs.
    maven { url = "https://maven.enginehub.org/repo/" }
    // Repository for the PlaceholderAPI, API.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing the 'PacketEvents' dependency.
    maven { url = "https://repo.codemc.io/repository/maven-releases" }
    // Repository containing all my libraries.
    maven { url = "https://vela.grabsky.workers.dev/releases" }
    // Repository containing the 'Azure' dependency.
    maven { url = "https://maven.pkg.github.com/grabsky/azure"; fillCredentials(it) }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
    // Paper
    paperweight.paperDevBundle("1.21.7-R0.1-SNAPSHOT")
    // Dependencies (My Libraries)
    paperLibrary("cloud.grabsky:bedrock:1.21-67")
    paperLibrary("cloud.grabsky:commands:1.20.1-40")
    paperLibrary("cloud.grabsky:configuration-paper:1.21.4-54")
    // WorldEdit
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.9-SNAPSHOT")
    // Azure
    compileOnly("cloud.grabsky:azure-api:1.20.1-11") { exclude group: "cloud.grabsky", module: "bedrock" }
    // PlaceholderAPI
    compileOnly("me.clip:placeholderapi:2.11.6")
    // PacketEvents
    compileOnly("com.github.retrooper:packetevents-spigot:2.8.0")
}

tasks {
    runServer {
        minecraftVersion("1.21.7")
        jvmArgs("--enable-preview")
    }
    shadowJar {
        shadowJar.archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        options.compilerArgs += "--enable-preview"
        // Setting compatibility to Java 21 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.claims.Claims"
    apiVersion = "1.21.7"
    serverDependencies {
        "PlaceholderAPI" { load = "BEFORE"; required = true }
        "packetevents"   { load = "BEFORE"; required = true }
        "WorldGuard"     { load = "BEFORE"; required = true }
        "Azure"          { load = "BEFORE"; required = true }
    }
}

// Configuring runServer task to use JetBrains' JDK 21 for expanded hot-swap features.
tasks.withType(xyz.jpenilla.runtask.task.AbstractRun.class).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-XX:+AllowEnhancedClassRedefinition")
}

// Returns formatted release name. (for GitHub Actions)
tasks.register("getRelease", () -> print("$VERSION #$RUN_NUMBER"))

// Returns formatted tag name. (for GitHub Actions)
tasks.register("getTag", () -> print("$VERSION+$RUN_NUMBER"))

/* UTILITY METHODS */

private final def fillCredentials(final MavenArtifactRepository repository) {
    repository.credentials {
        username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
        password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
    }
}
