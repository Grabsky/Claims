plugins {
    id("java-library")
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

group = "cloud.grabsky"
version = "0.9.2-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.enginehub.org/repo/" }
    maven { url = "https://repo.grabsky.cloud/snapshots" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.26")
    annotationProcessor("org.projectlombok:lombok:1.18.26")
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT")
    // Dependencies
    implementation("cloud.grabsky:bedrock:a1fb3fe")
    implementation("cloud.grabsky:commands:1.0")
    implementation("cloud.grabsky:configuration-paper:1ccfb9c")
    // WorldEdit
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.9-SNAPSHOT")
    // Azure
    compileOnly("cloud.grabsky:azure-api:58bbfc4")
}

tasks {
    shadowJar { archiveFileName = "${rootProject.name}-${project.version}.jar" }
}

tasks.register("deploy") {
    dependsOn(shadowJar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(shadowJar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(shadowJar) into(pluginsDirectory) }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.fork = true
}