plugins {
    id("java-library")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.papermc.paperweight.userdev") version "1.5.4"
}

final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

group = "cloud.grabsky"
version = "0.9.0-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.enginehub.org/repo/" }
    maven { url = "https://repo.grabsky.cloud/snapshots" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.26")
    annotationProcessor("org.projectlombok:lombok:1.18.26")
    // Paper
    paperweight.paperDevBundle("1.19.4-R0.1-SNAPSHOT")
    // Dependencies
    implementation("cloud.grabsky:bedrock:0b62651")
    implementation("cloud.grabsky:commands:14366de")
    implementation("cloud.grabsky:configuration-paper:1ccfb9c")
    // WorldEdit
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.7")
    // Azure
    compileOnly("cloud.grabsky:azure-api:5708fca")
}

tasks.register("deploy") {
    dependsOn(reobfJar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(reobfJar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(reobfJar) into(pluginsDirectory) }
    }
}