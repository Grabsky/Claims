plugins {
    id("java-library")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("net.minecrell.plugin-yml.paper") version "0.6.0"
}

final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

group = "cloud.grabsky"
version = "0.9.3-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.enginehub.org/repo/" }
    maven { url = "https://repo.grabsky.cloud/releases/" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.28")
    annotationProcessor("org.projectlombok:lombok:1.18.28")
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT")
    // Dependencies
    paperLibrary("cloud.grabsky:bedrock:1.20.1-47")
    paperLibrary("cloud.grabsky:commands:1.20.1-35")
    paperLibrary("cloud.grabsky:configuration-paper:1.20.1-42")
    // WorldEdit
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.9-SNAPSHOT")
    // Azure
    compileOnly("cloud.grabsky:azure-api:1.20.1-11") { exclude group: "cloud.grabsky", module: "bedrock" }
}

tasks {
    shadowJar { archiveFileName = "${rootProject.name}-${project.version}.jar" }
    compileJava {
        options.fork = true
        // Setting compatibility to Java 17 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks.register("deploy") {
    dependsOn(shadowJar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(shadowJar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(shadowJar) into(pluginsDirectory) }
    }
}

paper {
    name = "Claims"
    main = "cloud.grabsky.claims.Claims"
    loader = "cloud.grabsky.claims.Claims\$PluginLoader"
    apiVersion = "1.20"
    generateLibrariesJson = true
    serverDependencies {
        "WorldGuard" { load = "BEFORE"; required = true }
        "Azure"      { load = "BEFORE"; required = true }
    }
}